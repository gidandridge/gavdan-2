5 GOSUB 300
10 DIM S(8)
20 FOR X=0 TO 8 : S(X)=0 : NEXT X
30 S(4)=-1
35 PRINT
40 FOR Y=0 TO 6 STEP 3 : FOR X=0 TO 2
50 IF S(X+Y)=0 THEN PRINT ". ";
60 IF S(X+Y)=-1 THEN PRINT "* ";
70 NEXT X : PRINT : NEXT Y
75 IF S(0)+S(1)+S(2)+S(3)+S(4)+S(5)+S(6)+S(7)+S(8)=0 THEN PRINT "YOU LOSE":END
76 IF S(4)<>0 THEN GOTO 80
77 IF S(0)+S(1)+S(2)+S(3)+S(5)+S(6)+S(7)+S(8)=-8 THEN PRINT "YOU WIN":END
80 INPUT "YOUR SHOT (1 to 9)";T
90 IF T<1 OR T>9 THEN GOTO 80
95 LET T=T-1
100 IF S(T)<>-1 THEN PRINT "THAT WASN'T A VALID STAR NUMBER!" : GOTO 80
110 IF T=0 THEN S(0)=NOT(S(0)):S(1)=NOT(S(1)):S(3)=NOT(S(3)):S(4)=NOT(S(4))
120 IF T=1 THEN S(0)=NOT(S(0)):S(1)=NOT(S(1)):S(2)=NOT(S(2))
130 IF T=2 THEN S(1)=NOT(S(1)):S(2)=NOT(S(2)):S(4)=NOT(S(4)):S(5)=NOT(S(5))
140 IF T=3 THEN S(0)=NOT(S(0)):S(3)=NOT(S(3)):S(6)=NOT(S(6))
150 IF T<>4 THEN GOTO 170
160 S(1)=NOT(S(1)):S(3)=NOT(S(3)):S(4)=NOT(S(4)):S(5)=NOT(S(5)):S(7)=NOT(S(7))
170 IF T=5 THEN S(2)=NOT(S(2)):S(5)=NOT(S(5)):S(8)=NOT(S(8))
180 IF T=6 THEN S(3)=NOT(S(3)):S(4)=NOT(S(4)):S(6)=NOT(S(6)):S(7)=NOT(S(7))
190 IF T=7 THEN S(6)=NOT(S(6)):S(7)=NOT(S(7)):S(8)=NOT(S(8))
200 IF T=8 THEN S(4)=NOT(S(4)):S(5)=NOT(S(5)):S(7)=NOT(S(7)):S(8)=NOT(S(8))
210 GOTO 35
300 PRINT : PRINT : PRINT : PRINT
310 PRINT "S H O   SSS  TTT  AAA  RRR  SSS"
320 PRINT "        S     T   A A  R R  S"
330 PRINT "0 . T   SSS   T   AAA  RRR  SSS"
340 PRINT "          S   T   A A  RR     S"
350 PRINT "I N G   SSS   T   A A  R R  SSS"
360 PRINT
370 PRINT "**S H O O T I N G   S T A R S**"
380 PRINT "      A BRAIN TEASER GAME"
390 PRINT:PRINT "HIT RETURN TO CONTINUE ...":PRINT
400 DO:GET Q8$:LOOP UNTIL Q8$=CHR$(13)
410 PRINT "THERE ARE STARS    *"
420 PRINT "AND BLACK HOLES    ."
430 PRINT "IN THE UNIVERSE       . . ."
440 PRINT "                      . . ."
450 PRINT "                      . . ."
460 PRINT
470 PRINT "YOU SHOOT A STAR"
480 PRINT "(NOT A BLACK HOLE)"
490 PRINT "BY TYPING ITS NUMBER  1 2 3"
500 PRINT "                      4 5 6"
510 PRINT "                      7 8 9"
520 PRINT
530 PRINT "THAT CHANGES THE STAR TO A BLACK HOLE!"
540 PRINT:PRINT "HIT RETURN TO CONTINUE ...":PRINT
550 DO:GET Q8$:LOOP UNTIL Q8$=CHR$(13)
560 PRINT "EACH STAR IS IN A GALAXY. WHEN YOU"
570 PRINT "SHOOT A STAR, EVERYTHING IN ITS GALAXY"
580 PRINT "CHANGES. ALL STARS BECOME BLACK HOLES"
590 PRINT "AND ALL BLACK HOLES BECOME STARS."
600 PRINT
610 PRINT "GALAXIES:"
615 PRINT
620 PRINT "1 * .   * 2 *   . * 3   * . .   . * ."
630 PRINT "* * .   . . .   . * *   4 . .   * 5 *"
640 PRINT ". . .   . . .   . . .   * . .   . * ."
650 PRINT 
660 PRINT ". . *   . . .   . . .   . . ."
670 PRINT ". . 6   * * .   . . .   . * *"
680 PRINT ". . *   7 * .   * 8 *   . * 9"
690 PRINT:PRINT "HIT RETURN TO CONTINE ...":PRINT
700 DO:GET Q8$:LOOP UNTIL Q8$=CHR$(13)
710 PRINT "THE GAME STARTS     . . ."
720 PRINT "WITH THE UNIVERSE   . * ."
730 PRINT "LIKE THIS           . . ."
740 PRINT
750 PRINT "YOU WIN WHEN YOU    * * *"
760 PRINT "CHANGE IT TO THIS   * . *"
770 PRINT "                    * * *"
780 PRINT
790 PRINT "YOU LOOSE IF YOU    . . ."
800 PRINT "GET THIS            . . ."
810 PRINT "                    . . ."
815 PRINT
820 PRINT "READY TO PLAY? PRESS RETURN TO START"
830 PRINT "THE GAME. GOOD LUCK! ..."
840 DO:GET Q8$:LOOP UNTIL Q8$=CHR$(13)
900 RETURN
